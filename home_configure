#!/usr/bin/env fish

function has_string_changed -a key value
    if test ! -e vars/$key
        echo -n $value > vars/$key
        return 0
    end
    read -z last_value < vars/$key
    if test $value = $last_value
        return 1
    end
    echo -n $value > vars/$key
    return 0
end

function safe_symlink -a to from
    if test -L $from
        rm $from
    end
    ln -sT $to $from
end

function safe_rm_symlink -a link
    if test -L $link
        rm $link
    end
end

cd (dirname (status filename))

mkdir -p vars

if has_string_changed watchexec_version 2.3.2
    set dir (mktemp -d)
    and curl -L https://github.com/watchexec/watchexec/releases/download/v2.3.2/watchexec-2.3.2-x86_64-unknown-linux-musl.tar.xz \
        | tar -m -C $dir -J --strip-components=1 -x
    and mv $dir/watchexec $HOME/.local/bin/watchexec
    rm -r $dir
end

if test common.gitconfig -nt ~/.gitconfig
    echo -e "[user]\n\tname = Tom Mewett\n\temail = tom.mewett@codethink.co.uk" > ~/.gitconfig
    cat common.gitconfig >> ~/.gitconfig
end
