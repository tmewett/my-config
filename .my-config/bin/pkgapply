#!/usr/bin/env fish

argparse "h/help" "n/dry-run" -- $argv
or exit 1

# Print the script as a default --help action
function print_help
    test -e (status filename); or return
    echo "### file "(status filename)
    cat (status filename)
end

if set -q _flag_help
    print_help
    exit
end

function finish -a ret
    test -d $temp && rm -r $temp
    exit $ret
end

set temp (mktemp -d)
grep -vE '^\s*[#;]|^\s*$' $argv |sort |uniq >$temp/selections
pikaur -Qeq |sort >$temp/existing
or finish 1

echo -e "INSTALLED\n\tSPECIFIED"
comm --total -3 $temp/existing $temp/selections

if set -q _flag_n
    finish 0
end

pikaur -Qq |sort >$temp/installed
or finish 1

# installed n selections \ existing
set to_expl (comm -12 $temp/installed $temp/selections |comm -23 - $temp/existing)
if count $to_expl
    pikaur -D --asexplicit $to_expl
    or finish 1
end

# existing \ selections
set to_dep (comm -23 $temp/existing $temp/selections)
if count $to_dep
    pikaur -D --asdeps $to_dep
    or finish 1
end

# selections \ installed
set to_install (comm -13 $temp/installed $temp/selections)
if count $to_install
    pikaur -S $to_install
    or finish 1
end

finish 0
