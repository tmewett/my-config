#!/usr/bin/env fish

argparse "h/help" -- $argv
or exit 1
# Print the script as a default --help action
begin
    set -l file (status filename)
    if set -q _flag_help; and test -e $file
        set -l full_file (realpath $file)
        echo "### file $full_file"
        cat $full_file
        exit
    end
end

function print-image -a path
    if test -d $path
        for file in $path/*
            print-image $file
        end
    else
        echo "<div class='image'><img src='$path' loading='lazy'></div>"
    end
end

set html /tmp/gallery.html

begin
    echo "
    <!DOCTYPE html>
    <html>
    <head>
        <style>
            body {
                background-color: #333;
            }
            .image {
                min-height: 67vh;
            }
            img {
                display: block;
                max-width: 100%;
                max-height: 167vh;
                margin: auto;
            }
        </style>
    </head>
    <body>
    "

    for path in $argv
        print-image $path
    end

    echo "
    </body>
    </html>
    "
end > $html

firefox --private-window $html
