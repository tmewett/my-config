#!/usr/bin/env fish

function has_string_changed -a key value
    if test ! -e $hc_state/_hsc_$key
        set -q hc_dry_run || echo -n $value > $hc_state/_hsc_$key
        return 0
    end
    read -z last_value < $hc_state/_hsc_$key
    if test $value = $last_value
        return 1
    end
    set -q hc_dry_run || echo -n $value > $hc_state/_hsc_$key
    return 0
end

function safe_symlink -a to from
    if test -L $from
        echo "safe_symlink: removing existing symlink $from"
        set -q hc_dry_run || rm $from
    end
    echo "safe_symlink: creating $from -> $to"
    set -q hc_dry_run || ln -sT $to $from
end

function safe_rm_symlink -a link
    if test -L $link
        echo "safe_rm_symlink: removing $link"
        set -q hc_dry_run || rm $link
    end
end

argparse "n/dry-run" -- $argv
or exit 1
set -q _flag_n
and set -g hc_dry_run

set hc_state ~/.config/home-configure/state

mkdir -p $hc_state

function packages_pacman_get_explicitly_installed; pacman -Qeq; end
function packages_pacman_get_installed; pacman -Qq; end
function packages_pacman_mark_implicit; pacman -D --asdeps $argv; end
function packages_pacman_mark_explicit; pacman -D --asexplicit $argv; end
function packages_pacman_install; pacman -S $argv; end
function packages_apt_get_explicitly_installed; apt-mark showmanual; end
function packages_apt_get_installed; dpkg-query -f '${Package}\n' -W; end
function packages_apt_mark_implicit; sudo apt-mark auto $argv; end
function packages_apt_mark_explicit; sudo apt-mark manual $argv; end
function packages_apt_install; sudo apt install $argv; end
function packages_apply -a manager
    set selections $argv[2..]
    if not has_string_changed pkgapply_$manager "$selections"
        return
    end
    set explicit (packages_{$manager}_get_explicitly_installed)
    or return 1
    set installed (packages_{$manager}_get_installed)
    or return 1
    # installed n selections \ explicit
    for pkg in $installed
        if contains $pkg $selections; and not contains $pkg $explicit
            set -a make_explicit $pkg
        end
    end
    if set -q make_explicit
        echo "packages_apply $manager: marking as wanted: $make_explicit"
        set -q hc_dry_run || packages_{$manager}_mark_explicit $make_explicit
        or return 1
    end
    # explicit \ selections
    for pkg in $explicit
        if not contains $pkg $selections
            set -a make_implicit $pkg
        end
    end
    if set -q make_implicit
        echo "packages_apply $manager: marking as dependency: $make_implicit"
        set -q hc_dry_run || packages_{$manager}_mark_implicit $make_implicit
        or return 1
    end
    # selections \ installed
    for pkg in $selections
        if not contains $pkg $installed
            set -a install $pkg
        end
    end
    if set -q install
        echo "packages_apply $manager: installing $install"
        set -q hc_dry_run || packages_{$manager}_install $install
        or return 1
    end
end

source ~/home-configuration.fish

if set -q watchexec
    if has_string_changed watchexec_version 2.3.2
        set dir (mktemp -d)
        and curl -L https://github.com/watchexec/watchexec/releases/download/v2.3.2/watchexec-2.3.2-x86_64-unknown-linux-musl.tar.xz \
          | tar -m -C $dir -J --strip-components=1 -x
        and mv $dir/watchexec $HOME/.local/bin/watchexec
        rm -r $dir
    end
end

safe_rm_symlink ~/.config/fish/conf.d
