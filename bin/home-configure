#!/usr/bin/env fish

function hc_state_changed -a key value
    if test ! -e $hc_state/$key
        return 0
    end
    read -z last_value < $hc_state/$key
    if test $value = $last_value
        return 1
    end
    return 0
end

function hc_do -a message
    echo $message
    set -q hc_dry_run
end

function hc_state_save -a key value
    set -q hc_dry_run || echo -n $value > $hc_state/$key
end

function safe_symlink -a to from
    if test -L $from
        echo "safe_symlink: removing existing symlink $from"
        rm $from
    end
    if test -e $from
        ls -l $from
        test (read -P "delete $from? (y/N) ") = "y" || return
        rm -r $from
    end
    echo "safe_symlink: creating $from -> $to"
    mkdir -p (dirname $from)
    ln -sT $to $from
end

function safe_rm_symlink -a link
    if test -L $link
        echo "safe_rm_symlink: removing $link"
        set -q hc_dry_run || rm $link
    end
end

function hc_if -a message
    if test $status -eq 0
        not set -q hc_dry_run && $argv[2..]
    end
end

function hc_symlink_if -a to from
    if test $status -eq 0
        echo "symlinking $from -> $to"
        not set -q hc_dry_run; and safe_symlink $argv
    end
end

argparse "n/dry-run" -- $argv
or exit 1
set -q _flag_n
and set -g hc_dry_run

set hc_state ~/.config/home-configure/state

mkdir -p $hc_state

function packages_pacman_get_explicitly_installed; pacman -Qeq; end
function packages_pacman_get_installed; pacman -Qq; end
function packages_pacman_mark_implicit; pacman -D --asdeps $argv; end
function packages_pacman_mark_explicit; pacman -D --asexplicit $argv; end
function packages_pacman_install; pacman -S $argv; end
function packages_apt_get_explicitly_installed; apt-mark showmanual; end
function packages_apt_get_installed; dpkg-query -f '${Package}\n' -W; end
function packages_apt_mark_implicit; sudo apt-mark auto $argv; end
function packages_apt_mark_explicit; sudo apt-mark manual $argv; end
function packages_apt_install; sudo apt install $argv; end
function packages_apply -a manager
    set selections $argv[2..]
    if not hc_state_changed pkgapply_$manager "$selections"
        return
    end
    set explicit (packages_{$manager}_get_explicitly_installed)
    or return 1
    set installed (packages_{$manager}_get_installed)
    or return 1
    # installed n selections \ explicit
    for pkg in $installed
        if contains $pkg $selections; and not contains $pkg $explicit
            set -a make_explicit $pkg
        end
    end
    if set -q make_explicit
        echo "packages_apply $manager: marking as wanted: $make_explicit"
        set -q hc_dry_run || packages_{$manager}_mark_explicit $make_explicit
        or return 1
    end
    # explicit \ selections
    for pkg in $explicit
        if not contains $pkg $selections
            set -a make_implicit $pkg
        end
    end
    if set -q make_implicit
        echo "packages_apply $manager: marking as dependency: $make_implicit"
        set -q hc_dry_run || packages_{$manager}_mark_implicit $make_implicit
        or return 1
    end
    # selections \ installed
    for pkg in $selections
        if not contains $pkg $installed
            set -a install $pkg
        end
    end
    if set -q install
        echo "packages_apply $manager: installing $install"
        set -q hc_dry_run || packages_{$manager}_install $install
        or return 1
    end
end

source ~/home-configuration.fish

if set -q watchexec
    if hc_state_changed watchexec_version 2.3.2 && hc_do "watchexec: installing"
        set dir (mktemp -d)
        and curl -L https://github.com/watchexec/watchexec/releases/download/v2.3.2/watchexec-2.3.2-x86_64-unknown-linux-musl.tar.xz \
          | tar -m -C $dir -J --strip-components=1 -x
        and mv $dir/watchexec $HOME/.local/bin/watchexec
        and hc_state_save watchexec_version 2.3.2
        rm -r $dir
    end
end

function _dl_ta
    test -e $HOME/.textadept-bin
    and return
    set dir (mktemp -d)
    cd $dir
    curl -Lo https://github.com/orbitalquark/textadept/releases/download/textadept_12.7/textadept_12.7.linux.tgz
    and tar -xaf textadept_12.7.linux.tgz
    and sudo cp -rT textadept $HOME/.textadept-bin
    rm -r $dir
end
set -q textadept; hc_if _dl_ta
set -q textadept; hc_symlink_if $HOME/.textadept-bin/textadept $HOME/.local/bin/textadept

safe_rm_symlink ~/.config/fish/conf.d
