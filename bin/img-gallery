#!/usr/bin/env fish

argparse "h/help" "r" -- $argv
or exit 1
# Print the script as a default --help action
begin
    set -l file (status filename)
    if set -q _flag_help; and test -e $file
        set -l full_file (realpath $file)
        echo "### file $full_file"
        cat $full_file
        exit
    end
end

function print-image -a path
    if test -d $path
        for file in $path/*
            print-image $file
        end
    else if string match -q '*.mp4' $path
        echo "<div class='image'><video autoplay loop muted><source src='$path' type='video/mp4'></video></div>"
    else
        echo "<div class='image'><img src='$path' loading='lazy'></div>"
    end
end

set html /tmp/gallery.html
set files $argv/*
if set -q _flag_r
    set files (string join \n $files | shuf)
end

begin
    echo "
    <!DOCTYPE html>
    <html>
    <head>
        <style>
            body {
                background-color: #333;
            }
            .image {
                min-height: 67vh;
            }
            img, video {
                display: block;
                max-width: 100%;
                max-height: 167vh;
                margin: auto;
            }
        </style>
    </head>
    <body>
    "

    for path in $files
        print-image $path
    end

    echo "
    </body>
    </html>
    "
end > $html

firefox --private-window $html
